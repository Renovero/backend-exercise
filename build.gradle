buildscript {
    ext {
        springBootVersion = '2.0.0.M3'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "nu.studer:gradle-jooq-plugin:3.0.2"
        classpath "org.glassfish.jaxb:jaxb-runtime:2.3.1"
    }
}

plugins {
    id "org.flywaydb.flyway" version "7.3.2"
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'nu.studer.jooq'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 14

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jooq')
    compile "org.jooq:jooq"
    compile "com.h2database:h2"
    jooqRuntime "com.h2database:h2"
    jooqRuntime group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.1'
    jooqRuntime group: 'com.sun.activation', name: 'javax.activation', version: '1.2.0'
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile "io.rest-assured:rest-assured:3.0.3"
}

bootJar {
    mainClass = "com.exercise.BootApplication"
}

jooq {
    db(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = "jdbc:h2:file:$project.projectDir/db"
            user = 'sa'
            password = ''
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
            }
            generate {
                deprecated = false
                instanceFields = true
                records = true
            }
            target {
                packageName = 'com.exercise.generated'
                directory = 'build/generated/jooq'
            }
        }
    }
}

flyway {
    url = "jdbc:h2:file:$project.projectDir/db"
    user = 'sa'
    password = ''
    schemas = ['public']
    defaultSchema = 'public'
    locations = ["filesystem:$project.projectDir/src/main/resources/db/migration"]
}

task cleanDb() {
    delete 'data'
}

generateDbJooqSchemaSource.dependsOn cleanDb
generateDbJooqSchemaSource.dependsOn flywayMigrate
clean.dependsOn cleanDb
